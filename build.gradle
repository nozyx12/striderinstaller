plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "dev.nozyx.strider"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.json:json:20250517"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register("generateSourcesJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks.named("generateSourcesJar").get())

            groupId = group
            artifactId = "striderinstaller"
            version = version
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/repo")
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "StriderInstaller",
                "Implementation-Version": version,
                "Implementation-Vendor": "nozyx12",
                "Main-Class": "dev.nozyx.strider.installer.StriderInstaller"
        )
    }
}
